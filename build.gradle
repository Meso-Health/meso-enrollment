// Top-level build file where you can add configuration options common to all sub-projects/modules.

def versionMajor = 1
def versionMinor = 0
def versionPatch = 0
def versionCodeForLocalDevelopment = 1
def computedVersionCode = (System.getenv("VERSION_CODE") ?: versionCodeForLocalDevelopment) as Integer

// Configurations for gradle itself.
buildscript {
    repositories {
        google()
        jcenter()
    }

    ext {
        gradleVersion = '3.1.2'
        kotlinVersion = '1.2.20'
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

// Configurations for modules built by Gradle. Any dependency listed here will be shared by all
// modules.
allprojects {
    repositories {
        google()
        jcenter()
    }
}

// This block encapsulates custom properties and makes them available to all
// modules in the project.
ext {
    minSdkVersion = 24
    targetSdkVersion = 28
    compileSdkVersion = 28
    applicationId = "org.watsi.enrollment"
    versionCode = computedVersionCode
    versionName = "${versionMajor}.${versionMinor}.${versionPatch} (${computedVersionCode})"
}

// This adds more custom properties from the dependencies.gradle file and makes them available to
// all modules in the project.
apply from: 'dependencies.gradle'

task clean(type: Delete) {
    delete rootProject.buildDir
}

task runDomainUnitTests(dependsOn: [':domain:test']) {
    description 'Run unit tests for the domain module.'
}

task runDeviceUnitTests(dependsOn: [':device:testDebugUnitTest']) {
    description 'Run unit tests for the device module.'
}

task runAppUnitTests(dependsOn: [':app:testDemoDebugUnitTest', ':app:testUgandaDebugUnitTest']) {
    description 'Run unit tests for the app module.'
}

task runUnitTests(dependsOn: ['runDomainUnitTests', 'runDeviceUnitTests', 'runAppUnitTests']) {
    description 'Run unit tests for all modules.'
}

task runAppInstrumentationTests(dependsOn: [':app:connectedAndroidTest']) {
    description 'Run instrumentation tests for the app module.'
}
